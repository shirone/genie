dependencies {
    /*******************************
     * Compile Dependencies
     *******************************/

    compile project(":genie-common")

    compile("commons-beanutils:commons-beanutils:1.9.2")
    compile("com.squareup.retrofit2:retrofit:2.0.1")
    compile("com.squareup.retrofit2:converter-jackson:2.0.1")
    compile("org.apache.commons:commons-configuration2:2.0")

    // Logging
    compile("org.slf4j:slf4j-api")

    // JSON Patch Support
    compile("com.github.fge:json-patch")

    /*******************************
     * Provided Dependencies
     *******************************/

    /*******************************
     * Runtime Dependencies
     *******************************/

    /*******************************
     * Test Dependencies
     *******************************/

    testCompile(project(":genie-test"))
    testCompile(project(":genie-web"))
}

clean {
    delete "src/main/python/nflx_genie_client.egg-info"
    delete "src/main/python/dist"
}

task installPythonDependencies(type: Exec) {
    dependsOn clean
    workingDir "src/main/python"
    commandLine "sudo", "pip", "install", "-U", "pip", "setuptools", "twine"
}

task installPythonPackage(type: Exec) {
    dependsOn installPythonDependencies
    workingDir "src/main/python"
    commandLine "sudo", "pip", "install", "-U", "-e", "."
}

task testPythonPackage(type: Exec) {
    dependsOn installPythonPackage
    workingDir "src/main/python"
    commandLine "sh", "-c", "nosetests"
}

task buildPythonPackage(type: Exec) {
    dependsOn testPythonPackage
    workingDir "src/main/python"
    commandLine "sudo", "python", "setup.py", "sdist"
}

task uploadToPypi(type: Exec) {
    dependsOn buildPythonPackage
    workingDir "src/main/python"
    commandLine "sudo", "twine", "upload", "-r https://pypi.python.org/pypi", "-u $System.env.PYPI_USERNAME", "-p $System.env.PYPI_PASSWORD", "dist/*"
}
